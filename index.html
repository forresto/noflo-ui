<!DOCTYPE html>
<html>
  <head>
    <meta charset="utf-8">

    <title>NoFlo Development Environment</title>
    <link rel="shortcut icon" href="favicon.ico" />

    <!-- Polymer deps -->
    <script src="bower_components/platform/platform.js"></script>
    <link rel="import" href="bower_components/polymer/polymer.html">

    <!-- React -->
    <script src="bower_components/react/react.js"></script>

    <script src="browser/noflo-noflo-indexeddb/vendor/IndexedDBShim.min.js"></script>
    <link rel="import" href="bower_components/the-graph/the-graph-editor/the-graph-editor.html">
    <noflo-polymer name="the-graph-editor" inports="graph width height" outports="changed nodeclick edgeclick"></noflo-polymer>
    <link rel="import" href="browser/noflo-noflo-polymer/noflo-polymer/noflo-polymer.html">
    <link rel="import" href="elements/noflo-context.html">
    <noflo-polymer name="noflo-context" inports="editor project graphs component node edge buttons search clear getpanel" outports="project graphs currentgraph component newgraph nodes edges button result toolpanel contextpanel"></noflo-polymer>
    <link rel="import" href="elements/noflo-main.html">
    <noflo-polymer name="noflo-main" inports="open projects sketches examples graphs components route token" outports="newsketch newproject project graph component download githubtoken"></noflo-polymer>
    <link rel="import" href="elements/noflo-runtime.html">
    <noflo-polymer name="noflo-runtime" inports="graphs edges panel" outports="runtime"></noflo-polymer>
    <link rel="import" href="elements/noflo-search.html">
    <noflo-polymer name="noflo-search" inports="buttons results project graphs component panel" outports="menu search"></noflo-polymer>
    <link rel="import" href="elements/noflo-library.html">
    <noflo-polymer name="noflo-library" inports="buttons editor search" outports="button result"></noflo-polymer>
    <link rel="import" href="elements/noflo-account.html">
    <link rel="import" href="elements/noflo-project.html">
    <noflo-polymer name="noflo-project" inports="project graphs components githubtoken graphs component" outports="newgraph newcomponent changedproject upload"></noflo-polymer>
    <link rel="import" href="elements/noflo-component-editor.html">
    <noflo-polymer name="noflo-component-editor" inports="component graphs width height" outports="changed"></noflo-polymer>
    <script src="./browser/noflo-ui.js"></script>
    <link rel="stylesheet" href="bower_components/font-awesome/css/font-awesome.min.css">
    <link rel="stylesheet" href="./css/noflo-ui.css">
    <meta name="viewport" content="width=device-width, initial-scale=1.0, maximum-scale=1.0, minimum-scale=1.0, user-scalable=no" />
  </head>
  <body>
    <div id="container">
      <noflo-main></noflo-main>
      <noflo-context></noflo-context>
      <noflo-runtime></noflo-runtime>
      <noflo-search></noflo-search>
      <noflo-library></noflo-library>
      <noflo-project></noflo-project>
      <main id="workspace">
        <noflo-component-editor></noflo-component-editor>
        <the-graph-editor></the-graph-editor>
      </main>
    </div>
    <script type="application/fbp" id="PrepareStorage">
      EXPORT=OPEN.NAME:NAME
      EXPORT=ERRORS.OUT:ERROR
      EXPORT=SPLITDB.OUT:DB
      Open(indexeddb/Open) ERROR -> IN Errors(core/Merge)
      '1' -> VERSION Open
      Open DB -> IN SplitDb(core/Split)

      # Database upgrade setup
      Open UPGRADE -> UPGRADE Upgrade(indexeddb/UpgradeRouter)

      # ## Version 1

      # Project storage
      'projects' -> NAME CreateProjects(indexeddb/CreateStore)
      'id' -> KEYPATH CreateProjects
      Upgrade VERSIONS -> DB CreateProjects
      CreateProjects ERROR -> IN Errors

      # Graph storage
      'graphs' -> NAME CreateGraphs(indexeddb/CreateStore)
      'id' -> KEYPATH CreateGraphs
      CreateProjects DB -> DB CreateGraphs
      CreateGraphs ERROR -> IN Errors
      'project' -> NAME GraphProject(indexeddb/CreateIndex)
      'project' -> KEYPATH GraphProject
      CreateGraphs STORE -> STORE GraphProject
      # Migrate old LocalStorage data
      GraphProject STORE -> GRAPHSTORE Migrate(ui/MigrateLocalStorage)
      GraphProject ERROR -> IN Errors

      # Component storage
      'components' -> NAME CreateComponents(indexeddb/CreateStore)
      'id' -> KEYPATH CreateComponents
      CreateGraphs DB -> DB CreateComponents
      CreateComponents ERROR -> IN Errors
      'project' -> NAME ComponentProject(indexeddb/CreateIndex)
      'project' -> KEYPATH ComponentProject
      CreateComponents STORE -> STORE ComponentProject
      ComponentProject ERROR -> IN Errors
    </script>
    <script type="application/fbp" id="LoadData">
      EXPORT=READ.DB:DB
      EXPORT=MERGESTART.IN:LOAD
      EXPORT=GETPROJECTS.ITEM:PROJECTS
      EXPORT=GETGRAPHS.ITEM:GRAPHS
      EXPORT=GETCOMPONENTS.ITEM:COMPONENTS
      EXPORT=READ.COMPLETE:COMPLETE
      MergeStart(core/Merge) OUT -> IN SplitStart(core/Split)
      'readonly' -> DATA HoldMode(core/Kick) OUT -> MODE Read(indexeddb/BeginTransaction)
      SplitStart OUT -> IN HoldMode
      'projects,graphs,components' -> DATA HoldStores(core/Kick) OUT -> STORES Read
      SplitStart OUT -> IN HoldStores

      # Read projects list
      'projects' -> DATA HoldProjectStore(core/Kick) OUT -> NAME ProjectStore(indexeddb/GetStore)
      SplitStart OUT -> IN HoldProjectStore
      Read TRANSACTION -> TRANSACTION ProjectStore
      '' -> DATA HoldAllProjects(core/Kick) OUT -> ALL GetProjects(indexeddb/Query)
      SplitStart OUT -> IN HoldAllProjects
      ProjectStore STORE -> STORE GetProjects

      # Read graphs list
      'graphs' -> DATA HoldGraphStore(core/Kick) OUT -> NAME GraphStore(indexeddb/GetStore)
      SplitStart OUT -> IN HoldGraphStore
      ProjectStore TRANSACTION -> TRANSACTION GraphStore
      '' -> DATA HoldAllGraphs(core/Kick) OUT -> ALL GetGraphs(indexeddb/Query)
      GetProjects ITEM -> IN HoldAllGraphs
      GraphStore STORE -> STORE GetGraphs

      # Read components list
      'components' -> DATA HoldComponentStore(core/Kick) OUT -> NAME ComponentStore(indexeddb/GetStore)
      SplitStart OUT -> IN HoldComponentStore
      GraphStore TRANSACTION -> TRANSACTION ComponentStore
      '' -> DATA HoldAllComponents(core/Kick) OUT -> ALL GetComponents(indexeddb/Query)
      GetProjects ITEM -> IN HoldAllComponents
      ComponentStore STORE -> STORE GetComponents
    </script>
    <script type="application/fbp" id="StoreGraph">
      EXPORT=MERGE.IN:ITEM
      EXPORT=HOLDDB.DATA:DB
      EXPORT=PUTGRAPH.KEY:KEY
      Merge(core/Merge) OUT -> IN DropExamples(ui/DropExamples)
      DropExamples OUT -> IN Disc(core/DisconnectAfterPacket) OUT -> IN SplitGraph(core/Split)
      SplitGraph OUT -> IN HoldDb(core/Kick) OUT -> DB Read(indexeddb/BeginTransaction)
      'readwrite' -> DATA HoldMode(core/Kick) OUT -> MODE Read
      SplitGraph OUT -> IN HoldMode
      'graphs' -> DATA HoldStore(core/Kick) OUT -> IN SplitStore(core/Split) OUT -> STORES Read
      SplitGraph OUT -> IN HoldStore
      SplitStore OUT -> NAME GraphStore(indexeddb/GetStore)
      Read TRANSACTION -> TRANSACTION GraphStore STORE -> STORE PutGraph(indexeddb/Put)
      'project' -> KEY GetProject(objects/GetObjectKey)
      SplitGraph OUT -> IN GetProject OBJECT -> IN MergeStorable(core/Merge) OUT -> VALUE PutGraph
      GetProject OUT -> IN DropProject(core/Drop)
      '' -> DATA HoldProject(core/Kick) OUT -> VALUE SetProject(objects/SetPropertyValue)
      SplitGraph OUT -> IN HoldProject
      'project' -> PROPERTY SetProject
      GetProject MISSED -> IN SetProject OUT -> IN MergeStorable
    </script>
    <script type="application/fbp" id="LoadProject">
      EXPORT=HOLDDB.DATA:DB
      EXPORT=SPLITID.IN:ID
      EXPORT=GET.ITEM:PROJECT
      SplitId(core/Split) OUT -> IN HoldDb(core/Kick) OUT -> DB Read(indexeddb/BeginTransaction)
      'readonly' -> DATA HoldMode(core/Kick) OUT -> MODE Read
      SplitId OUT -> IN HoldMode
      'projects' -> DATA HoldStore(core/Kick) OUT -> IN SplitStore(core/Split) OUT -> STORES Read
      SplitId OUT -> IN HoldStore
      SplitStore OUT -> NAME Store(indexeddb/GetStore)
      Read TRANSACTION -> TRANSACTION Store STORE -> STORE Get(indexeddb/Get)
      SplitId OUT -> KEY Get
    </script>
    <script type="application/fbp" id="PrepareProject">
      EXPORT=GENERATEID.IN:IN
      EXPORT=STOREGRAPH.DB:DB
      EXPORT=SETMAIN.OUT:OUT
      GenerateId(local/GenerateId) OUT -> IN Split(core/Split)
      'id' -> KEY GetId(objects/GetObjectKey)
      'project' -> PROPERTY SetProject(objects/SetPropertyValue)
      GetId OUT -> VALUE SetProject
      Split OUT -> IN GetId
      'name' -> KEY GetName(objects/GetObjectKey)
      'name' -> PROPERTY SetName(objects/SetPropertyValue)
      GetName OUT -> VALUE SetName
      Split OUT -> IN GetName
      'type' -> KEY GetType(objects/GetObjectKey)
      'type' -> PROPERTY SetType(objects/SetPropertyValue)
      GetType OUT -> VALUE SetType
      Split OUT -> IN GetType
      Split OUT -> START InitializeGraph(objects/CreateObject)
      InitializeGraph OUT -> IN SetName OUT -> IN SetType OUT -> DETAILS ToGraph(ui/CreateGraph)
      'toJSON' -> METHOD Convert(objects/CallMethod)
      ToGraph OUT -> IN Convert OUT -> IN SetProject OUT -> IN GenGraphId(local/GenerateId)
      GenGraphId OUT -> ITEM StoreGraph(local/StoreGraph)
      'main' -> PROPERTY SetMain(objects/SetPropertyValue)
      StoreGraph KEY -> IN SplitGraphId(core/Split) OUT -> VALUE SetMain
      Split OUT -> DATA HoldProject(core/Kick) OUT -> IN SetMain
      SplitGraphId OUT -> IN HoldProject
    </script>
    <script type="application/fbp" id="StoreProject">
      EXPORT=DISC.IN:ITEM
      EXPORT=HOLDDB.DATA:DB
      EXPORT=PUTPROJECT.KEY:KEY
      Disc(core/DisconnectAfterPacket) OUT -> IN Split(core/Split)
      Split OUT -> IN HoldDb(core/Kick) OUT -> DB Read(indexeddb/BeginTransaction)
      'readwrite' -> DATA HoldMode(core/Kick) OUT -> MODE Read
      Split OUT -> IN HoldMode
      'projects' -> DATA HoldStore(core/Kick) OUT -> IN SplitStore(core/Split) OUT -> STORES Read
      Split OUT -> IN HoldStore
      SplitStore OUT -> NAME Store(indexeddb/GetStore)
      Read TRANSACTION -> TRANSACTION Store STORE -> STORE PutProject(indexeddb/Put)
      Split OUT -> VALUE PutProject
    </script>
    <script type="application/fbp" id="StoreComponent">
      EXPORT=MERGE.IN:ITEM
      EXPORT=HOLDDB.DATA:DB
      EXPORT=PUT.KEY:KEY
      Merge(core/Merge) OUT -> IN Disc(core/DisconnectAfterPacket) OUT -> IN Split(core/Split)
      Split OUT -> IN HoldDb(core/Kick) OUT -> DB Read(indexeddb/BeginTransaction)
      'readwrite' -> DATA HoldMode(core/Kick) OUT -> MODE Read
      Split OUT -> IN HoldMode
      'components' -> DATA HoldStore(core/Kick) OUT -> IN SplitStore(core/Split) OUT -> STORES Read
      Split OUT -> IN HoldStore
      SplitStore OUT -> NAME Store(indexeddb/GetStore)
      Read TRANSACTION -> TRANSACTION Store STORE -> STORE Put(indexeddb/Put)
      Split OUT -> VALUE Put
    </script>
    <script type="application/fbp" id="LoadExample">
      EXPORT=SPLITGIST.IN:GIST
      EXPORT=PARSEEXAMPLE.OUT:GRAPH
      EXPORT=ERRORS.OUT:ERROR
      'https://api.github.com/gists' -> STRING GistPrefix(strings/SendString)
      '/' -> DELIMITER CreateUrl(strings/CompileString)
      SplitGist(core/Split) OUT -> IN GistPrefix
      GistPrefix OUT -> IN CreateUrl
      SplitGist OUT -> IN CreateUrl
      CreateUrl OUT -> URL GetGist(ajax/GetJsonP)
      'data' -> KEY GetData(objects/GetObjectKey)
      'files' -> KEY GetFiles(objects/GetObjectKey)
      'noflo.json' -> KEY GetFile(objects/GetObjectKey)
      'content' -> KEY GetFileContent(objects/GetObjectKey)
      GetGist OUT -> IN GetData
      GetData OUT -> IN GetFiles OUT -> IN GetFile OUT -> IN GetFileContent
      GetFiles MISSED -> IN Errors
      GetFileContent OUT -> IN ParseExample(strings/ParseJson)
    </script>
    <script type="application/fbp" id="GenerateId">
      EXPORT=SPLITITEM.IN:IN
      EXPORT=SETID.OUT:OUT
      'id' -> PROPERTY SetId(objects/SetPropertyValue)
      SplitItem(core/Split) OUT -> START Generate(ui/GenerateId)
      Generate OUT -> VALUE SetId
      SplitItem OUT -> IN SetId
    </script>
    <script type="application/fbp" id="GithubUpload">
      EXPORT=REPO.IN:REPO
      EXPORT=TOKEN.IN:TOKEN
      EXPORT=UPLOAD.PATH:NAME
      EXPORT=UPLOAD.MESSAGE:MESSAGE
      EXPORT=WAITREPO.STRING:CONTENT

      # Plain repository name for repo creation
      '(.*)\/(.*)' -> PATTERN RepoName(strings/Replace)
      '$2' -> REPLACEMENT RepoName
      Repo(core/Repeat) OUT -> IN RepoName(strings/Replace)
      RepoName OUT -> DATA StartCreate(core/Kick)

      # Check if repository exists
      Token(core/Repeat) OUT -> TOKEN CheckRepo
      Repo OUT -> IN CheckRepo(github/GetRepository)
      CheckRepo ERROR -> IN Show(core/Output) OUT -> IN StartCreate
      # If not, we create it
      Token OUT -> TOKEN CreateRepo(github/CreateRepository)
      StartCreate OUT -> IN CreateRepo

      # Once we have repository we can upload the document
      Token OUT -> TOKEN Upload
      Repo OUT -> REPOSITORY Upload(github/SetContents)
      WaitRepo(strings/SendString) OUT -> IN Upload

      # Wait until the repository is ready
      CheckRepo OUT -> IN WaitRepo
      CreateRepo OUT -> IN WaitRepo

      Upload OUT -> IN Show(core/Output)
    </script>
    <script type="application/fbp" id="Upload">
      EXPORT=GETTOKEN.IN:IN
      'token' -> KEY GetToken(objects/GetObjectKey) OUT -> TOKEN Upload(local/GithubUpload)
      'repo' -> KEY GetRepo(objects/GetObjectKey) OUT -> REPO Upload
      'doc' -> KEY GetDoc(objects/GetObjectKey) OUT -> IN MakePath(ui/MakePath) OUT -> NAME Upload
      'message' -> KEY GetMessage(objects/GetObjectKey) OUT -> MESSAGE Upload
      'content' -> KEY GetContent(objects/GetObjectKey) OUT -> CONTENT Upload
      GetToken OBJECT -> IN GetRepo OBJECT -> IN GetDoc OBJECT -> IN GetMessage OBJECT -> IN GetContent
    </script>
    <script type="application/fbp" id="ProjectContext">
      EXPORT=CONTEXT.PROJECT:PROJECT
      EXPORT=CONTEXT.GRAPHS:GRAPHS
      EXPORT=CONTEXT.COMPONENT:COMPONENT
      EXPORT=CONTEXT.CLEAR:CLEAR
      EXPORT=GRAPHEDITOR.CHANGED:GRAPHCHANGED
      EXPORT=COMPONENTEDITOR.CHANGED:COMPONENTCHANGED
      EXPORT=MERGENEWGRAPH.OUT:NEWGRAPH
      EXPORT=PROJECT.GITHUBTOKEN:GITHUBTOKEN
      EXPORT=PROJECT.NEWCOMPONENT:NEWCOMPONENT
      EXPORT=PROJECT.CHANGEDPROJECT:CHANGEDPROJECT
      EXPORT=PROJECT.UPLOAD:UPLOAD
      EXPORT=LOADPROJECTDATA.DB:DB
      'noflo-context' -> SELECTOR Context(polymer/noflo-context)

      # Runtime handling
      'noflo-runtime' -> SELECTOR Runtime(polymer/noflo-runtime)
      Context GRAPHS -> GRAPHS Runtime
      Context EDGES -> EDGES Runtime

      # Search handling
      'noflo-search' -> SELECTOR Search(polymer/noflo-search)
      Context GRAPHS -> GRAPHS Search
      Context COMPONENT -> COMPONENT Search
      Context PROJECT -> PROJECT Search

      # Panel handling
      '' -> GETPANEL Context TOOLPANEL -> PANEL Runtime
      Context CONTEXTPANEL -> PANEL Search

      # Library handling
      'noflo-library' -> SELECTOR Library(polymer/noflo-library)

      # Main menu
      Search MENU -> BUTTONS Context BUTTON -> BUTTONS Search
      Search MENU -> BUTTONS Library BUTTON -> BUTTONS Search

      # UI searching
      Search SEARCH -> SEARCH Library RESULT -> RESULTS Search
      Search SEARCH -> SEARCH Context RESULT -> RESULTS Search

      # Project menu
      'noflo-project' -> SELECTOR Project(polymer/noflo-project)
      Context PROJECT -> PROJECT Project
      Context GRAPHS -> GRAPHS Project
      Context COMPONENT -> COMPONENT Project
      Project NEWGRAPH -> IN MergeNewGraph(core/Merge)
      Context NEWGRAPH -> IN MergeNewGraph

      # Prepare graph editor
      Context CURRENTGRAPH -> CURRENTGRAPH GraphEditor(local/GraphEditor)
      Runtime RUNTIME -> RUNTIME GraphEditor
      Context NEWGRAPH -> NEWGRAPH GraphEditor
      Context PROJECT -> PROJECT GraphEditor
      GraphEditor NODECLICK -> NODE Context
      GraphEditor EDGECLICK -> EDGE Context
      GraphEditor EDITOR -> EDITOR Library
      GraphEditor EDITOR -> EDITOR Context

      # Prepare component editor
      'noflo-component-editor' -> SELECTOR ComponentEditor(polymer/noflo-component-editor)
      Context COMPONENT -> COMPONENT ComponentEditor
      Context GRAPHS -> GRAPHS ComponentEditor

      # Listen for screen size changes and communicate with editors
      '' -> START ListenWindow(interaction/ListenResize)
      ListenWindow WIDTH -> WIDTH GraphEditor
      ListenWindow HEIGHT -> HEIGHT GraphEditor
      ListenWindow WIDTH -> WIDTH ComponentEditor
      ListenWindow HEIGHT -> HEIGHT ComponentEditor
    </script>
    <script type="application/fbp" id="GraphEditor">
      EXPORT=LOADEDITOR.CONTAINER:WORKSPACE
      EXPORT=CONNECTRUNTIME.RUNTIME:RUNTIME
      EXPORT=CONNECTRUNTIME.PROJECT:PROJECT
      EXPORT=CONNECTRUNTIME.NEWGRAPH:NEWGRAPH
      EXPORT=GRAPHEDITOR.GRAPH:CURRENTGRAPH
      EXPORT=GRAPHEDITOR.WIDTH:WIDTH
      EXPORT=GRAPHEDITOR.HEIGHT:HEIGHT
      EXPORT=SPLITEDITOR.OUT:EDITOR
      EXPORT=GRAPHEDITOR.NODECLICK:NODECLICK
      EXPORT=GRAPHEDITOR.EDGECLICK:EDGECLICK
      EXPORT=CONVERT.OUT:CHANGED
      'the-graph-editor' -> SELECTOR GraphEditor(polymer/the-graph-editor)
      GraphEditor ELEMENT -> IN Async(core/RepeatAsync) OUT -> IN SplitEditor(core/Split)
      SplitEditor OUT -> EDITOR ConnectRuntime(ui/ConnectRuntime)
      'toJSON' -> METHOD Convert(objects/CallMethod)
      GraphEditor CHANGED -> IN Convert
    </script>
    <script type="application/fbp" id="main">
      'noflo-ui' -> NAME Storage(local/PrepareStorage)

      # Prepare the main view
      'noflo-main' -> SELECTOR Main(polymer/noflo-main)
      Storage DB -> DB LoadData(local/LoadData)
      Storage ERROR -> IN ShowErrors(core/Output)
      '' -> LOAD LoadData
      LoadData PROJECTS -> PROJECTS Main
      LoadData GRAPHS -> GRAPHS Main
      LoadData COMPONENTS -> COMPONENTS Main
      Storage DB -> DB StoreNewGraph(local/StoreGraph)
      Main NEWSKETCH -> IN NewGraphId(local/GenerateId) OUT -> ITEM StoreNewGraph
      Storage DB -> DB StoreProject(local/StoreProject)
      Storage DB -> DB PrepareProject(local/PrepareProject)
      Main NEWPROJECT -> IN PrepareProject OUT -> ITEM StoreProject

      # Prepare workspace
      #'#workspace' -> SELECTOR GetWorkspace(dom/GetElement) ELEMENT -> IN Workspace(core/Split)
      #Workspace OUT -> WORKSPACE Context

      # Prepare the context
      Router(ui/Router) ROUTE -> CLEAR Context(local/ProjectContext)
      Router ROUTE -> ROUTE Main
      Main PROJECT -> PROJECT Context
      Main GRAPH -> GRAPHS Context
      Main COMPONENT -> COMPONENT Context
      Storage DB -> DB StoreComponent(local/StoreComponent)
      Context COMPONENTCHANGED -> IN SplitChangedComponent(core/Split) OUT -> ITEM StoreComponent
      SplitChangedComponent OUT -> COMPONENTS Main
      Storage DB -> DB StoreGraph(local/StoreGraph)
      'id' -> KEY CheckGraphID(objects/GetObjectKey)
      Context GRAPHCHANGED -> IN Check(ui/IgnoreExamples) OUT -> IN SplitChangedGraph(core/Split) OUT -> ITEM StoreGraph
      SplitChangedGraph OUT -> GRAPHS Main
      Context NEWGRAPH -> IN NewProjectGraphId(local/GenerateId) OUT -> ITEM StoreGraph
      Context NEWCOMPONENT -> IN NewProjectComponentId(local/GenerateId) OUT -> ITEM StoreComponent
      Context CHANGEDPROJECT -> ITEM StoreProject

      # Handle GitHub tokens
      '' -> TOKEN Main GITHUBTOKEN -> GITHUBTOKEN Context
      Main DOWNLOAD -> IN Show(core/Output)
      Context UPLOAD -> IN Upload(local/Upload)

      # Example handling
      Router EXAMPLE -> GIST LoadExample(local/LoadExample)
      LoadExample GRAPH -> GRAPHS Context

      # Show/hide main widget
      'true' -> DATA ShowMain(core/Kick) OUT -> OPEN Main
      'false' -> DATA HideMain(core/Kick) OUT -> OPEN Main
      Router MAIN -> IN ShowMain
      Router ROUTE -> IN HideMain

      # Routing
      LoadData COMPLETE -> START ListenHash(interaction/ListenHash)
      ListenHash INITIAL -> URL Router
      ListenHash CHANGE -> URL Router
    </script>
    <script src="app/noflo.js"></script>
  </body>
</html>
